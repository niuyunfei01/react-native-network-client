
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
}

dependencies {
    compile "com.android.support:support-v4:23.1.0"
    compile "com.android.support:gridlayout-v7:23.1.0"
    compile "com.android.support:cardview-v7:23.1.0"
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:appcompat-v7:18.0.+'

    compile 'in.srain.cube:ultra-ptr:1.0.11'

    compile fileTree(dir: 'libs', include: '*.jar', exclude: ['*-javadoc.jar', '*-sources.jar'])
}

// The sample build uses multiple directories to
// keep boilerplate and common code separate from
// the print sample code.
List<String> dirs = [
    'print',     // print sample code; look here for the interesting stuff.
    'common',   // components that are reused by multiple samples
    'template'] // boilerplate code that is generated by the sample template process

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            dirs.each { dir ->
                java.srcDirs "src/${dir}/java"
                res.srcDirs "src/${dir}/res"
            }

            jniLibs.srcDirs = ['libs']
        }

        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']

    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'res/drawable-*-v4/*'
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            if (System.getenv("KEYSTORE") != null) {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "applicationId", "cn.cainiaoshicai.crm" + getApplicationIdSuffix()
            resValue "string", "weiciyuanOfficalWeiboName",
                    "cn.cainiaoshicai.crm" + getApplicationIdSuffix() + ".mention://@Cainiao-CRM"
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            resValue "string", "applicationId", "cn.cainiaoshicai.crm"
            resValue "string", "Cainiaoshicai.cn",
                    "cn.cainiaoshicai.crm" + ".mention://@Cainiao-CRM"
            debuggable false
            signingConfig signingConfigs.release
        }

    }
}
